.MCALL	.MODULE
.MODULE	WD,VERSION=01,COMMENT=<MC0511 Hard Disk Handler>,AUDIT=YES

;TIM$IT	=	1
;UNI$64	=	1

.IIF	NDF	UNI$64	UNI$64=0

.IF	NE	UNI$64
WD$N64	=	1
.IFF
WD$N64	=	0
.ENDC

.IF	NE	WD$N64
	.LIBRAR	/SY:SYSTEM/
	.MCALL	.CF3DF,.FIXDF,.SYCDF

	.CF3DF
	.FIXDF
	.SYCDF
.ENDC	;NE	WD$N64

	.MCALL	.ADDR,.ASSUME,.DRDEF
.IF	NE	WD$N64
	.DRDEF	WD,367,FILST$!VARSZ$!SPFUN$,0,176670,144,UNIT64=YES
	.DRPTR	FETCH=INIT,LOAD=INIT,RELEASE=DONE,UNLOAD=DONE
.IFF	;NE	WD$N64
	.DRDEF	WD,367,FILST$!VARSZ$!SPFUN$,0,176670,144
.ENDC	;NE	WD$N64

	.DRINS	WD
	NOP

	JSR	R0,INSCK
	.WORD	0		;BLKN
	.BYTE	373,0		;FUNC,UNIT
	.WORD	0		;BUFF
	.WORD	1		;WCNT

WDIQE:	.WORD	0
	.BLKW	4
WDRTI:	RTI

INSCK:	MOV	R0,Q$BUFF(R0)
	MOV	@#WD$VEC,@SP
	MOV	@#WD$VEC+2,-(SP)
	MOV	R0,WDIQE
	.ADDR	#WDRTI,R1
	MOV	R1,@#WD$VEC
	MOV	#342,@#WD$VEC+2
	MOV	#10000,R2
10$:	TSTB	@#WD$VEC+2
	BPL	20$
	SOB	R2,10$
	SEC
20$:	MOV	(SP)+,@#WD$VEC+2
	MOV	(SP)+,@#WD$VEC
	RETURN

.ASSUME	. LE 400,MESSAGE=<;Install area overflow>

	.DRBEG	WD
	MOV	WDCQE,R4

.IF	NE	WD$N64
	MOVB	Q$UNIT(R4),R1
	BIC	#^C7,R1
	CLR	R3
	MOVB	Q$FUNC(R4),R2
	BPL	10$
	MOVB	R2,R3
	BIC	#^C17,R3
	BIS	#360,R3
	COMB	R2
10$:	BIC	#^C160,R2
	ASR	R2
	BISB	R2,R1
	MOV	Q$FUNC(R4),(PC)+
SAVFU:	.WORD	0
	MOVB	R1,Q$UNIT(R4)
	MOVB	R3,Q$FUNC(R4)
.ENDC	;NE	WD$N64

	MOV	R4,WDRQE
	TST	(PC)+
WDRQE:	.WORD	0
	MOV	#342,@#WD$VEC+2

	.DRAST	WD,2
	MOV	WDCQE,R4
.IF	NE	WD$N64
	MOV	SAVFU,Q$FUNC(R4)
.ENDC	;NE	WD$N64
	BISB	@#WD$VEC+2,@-(R4)
	.DRFIN	WD

	.DRBOT	WD,BOOT,RDBUF
.=WDBOOT+100
	.WORD	102,2
.=WDBOOT+210
RDBUF:	MOV	#SBREAD-WDBOOT,R3
	MOV	R0,(R3)+
	INC	R3
	MOVB	@#B$DEVU,(R3)+
	MOV	R2,(R3)+
	MOV	R1,(R3)+
RD.BUF:
	MOV	@#WD$VEC,-(SP)
	MOV	#SB$LC-WDBOOT,@#WD$VEC
	MOV	#342,@#WD$VEC+2
10$:	TSTB	@#WD$VEC+2
	BMI	10$
	BEQ	20$
	JMP	@#BIOERR
20$:	MOV	(SP)+,@#WD$VEC
	RETURN

BOOT:	MOV	#10000,SP
	MOVB	R0,D.NUM
	MOV	R0,R5
	CALL	RD.BUF
	MOV	#RDBUF-WDBOOT,@#B$READ
	MOV	#B$DNAM,@#B$DEVN
	MOV	R5,@#B$DEVU
	JMP	@#B$BOOT

SBREAD:	.WORD	2
	.BYTE	0
D.NUM:	.BYTE	-1
	.WORD	1000
	.WORD	400*4
	.WORD	SBREAD-WDBOOT
	.BLKB	8.
SB$LC:	RTI

	.DREND	WD

	.PSECT	SETOVR
.IF	NE	WD$N64
INIT:	CALL	FIXOWN
	BEQ	QUIT
	ADD	#WD$X64-WDLQE,@R1
QUIT:	CLC
	RETURN

DONE:	CALL	FIXOWN
	BEQ	QUIT
	CLR	@R1
	BR	QUIT

FIXOWN:	MOV	@#$SYPTR,R1
	BIT	#CF3.64,$CNFG3(R1)
	BEQ	10$
	BIT	#CF3.OW,$CNFG3(R1)
	BEQ	10$
	MOV	R2,R3
	ASL	R3
	ASL	R3
	ADD	R2,R3
	MOV	$PNPTR(R1),-(SP)
	ADD	R1,@SP
	ADD	R2,@SP
	MOV	R5,R1
	SUB	(SP)+,R1
	ADD	R5,R1
	SUB	RLSR1
	CMP	-(R1),-(R1)
	MOV	@R5,-(R1)
10$:	RETURN
.ENDC	;NE	WD$N64

	.END
